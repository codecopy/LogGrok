using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.WPF;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Text.RegularExpressions;

using LogGrok.Core;

namespace LogGrok.Search
{
    [Record]
    [NotifyPropertyChanged]
    class SearchPattern
    {
        [Record]
        class PatternValidationResult
        {
            public static GetValidResult(pattern : string) : PatternValidationResult
            {
                PatternValidationResult(pattern, true, string.Empty)
            }

            [Accessor] _pattern : string;
            [Accessor] _isValidRegex : bool;
            [Accessor] _errorMessage : string;
        }

        public Text          : string { get; set }
        public CaseSensitive : bool { get; set;}
        public UseRegex      : bool { get; set }

        public GetRegex() : option[Regex]
        {
            GetRegex(RegexOptions.None)
        }

        public Clone() : SearchPattern
        {
            SearchPattern(Text, CaseSensitive, UseRegex)
        }
        
        public GetRegex(regexAdditionalOptions : RegexOptions) : option[Regex]
        {
            if (!IsValid || string.IsNullOrWhiteSpace(Text))
            {
                None()
            }
            else
            {
                def regexOptions = if (CaseSensitive) RegexOptions.None else RegexOptions.IgnoreCase;
                def pattern = if (UseRegex) Text else Regex.Escape(Text);
                Some(Regex(pattern, regexOptions | regexAdditionalOptions))
            }
        }

        public IsValid : bool
        {
            public get 
            {
                if (!UseRegex)
                {
                    true
                }
                else
                {
                    ValidateRegex();
                    _patternValidationResult.IsValidRegex;
                }
            }
        }

        public ValidationError : string 
        { 
            get
            {
                if (!UseRegex)
                {
                    string.Empty
                }
                else
                {
                    ValidateRegex();
                    _patternValidationResult.ErrorMessage;
                }
            }
        }

        public GetSearchOptions() : SearchOptions
        {
            mutable options = SearchOptions.None;
            when (CaseSensitive)
                options |= SearchOptions.CaseSensitive;

            when (UseRegex) 
                options |= SearchOptions.UseRegex;

            options
        }

        private ValidateRegex() : void
        {
            unless (_patternValidationResult.Pattern == Text)
            {
                try
                {
                    _ = Regex(Text??string.Empty);
                    _patternValidationResult = PatternValidationResult.GetValidResult(Text);
                }
                catch
                {
                    | e => _patternValidationResult = PatternValidationResult(Text, false, e.Message);
                }
            }
        }

        [RecordIgnore]
        mutable _patternValidationResult : PatternValidationResult = PatternValidationResult.GetValidResult(string.Empty);
    }
}
