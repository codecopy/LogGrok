using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.WPF;
using Nemerle.Extensions;

using System;
using System.Windows;
using System.Windows.Controls;
using System.Collections.ObjectModel;
using System.Linq;

using Xceed.Wpf.AvalonDock;

namespace LogGrok.Infrastructure
{
  [Record]
  public class MenuItemCollection : Collection[Control]
  {
      protected override InsertItem(_ : int, item : Control) : void
      {
	    _itemColection.Insert(_lastIndex, item);
	    _lastIndex++;
      }
      
      [RecordIgnore] mutable _lastIndex : int;
      _itemColection : ItemCollection;
  }
  
  public module DocumentContextMenu
  {
      public AdditionalContextMenuItemsProperty : DependencyProperty =
        DependencyProperty.RegisterAttached(
            "AdditionalContextMenuItemsInternal",
            typeof(MenuItemCollection),
            typeof(DocumentContextMenu));

      public GetAdditionalContextMenuItems(d : DockingManager) : MenuItemCollection
      {
          match(d.GetValue(AdditionalContextMenuItemsProperty) :> MenuItemCollection)
          {
              | null with collection = MenuItemCollection(d.DocumentContextMenu.Items) => 
                d.SetValue(AdditionalContextMenuItemsProperty, collection);
                collection
                
              | collection => collection
          }
        }
  }
}
