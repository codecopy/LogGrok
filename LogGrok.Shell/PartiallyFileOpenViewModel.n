using System;
using System.Text;
using System.Collections.Generic;
using System.Linq;
using System.IO;
using Microsoft.Practices.Prism.Commands;
using System.Windows.Input;
using System.Windows;

using Nemerle.WPF;
using Nemerle.Surround;

using LogGrok.Core;
using LogGrok.Core.EncodingDetector;

[assembly:DefineSurround("seekToBegin", true, _ = _stream.Seek(0, SeekOrigin.Begin), _ = _stream.Seek(0, SeekOrigin.Begin))]

namespace LogGrok.Shell
{
    [NotifyPropertyChanged]
    class PartiallyFileOpenViewModel
    {
        public this(fileName : string, view : Window)
        {
            def Open()
            {
                view.DialogResult = true;
                view.Close();
                _stream.Dispose();
            }
            
			_stream = LogFile.OpenRead(fileName);
			
			surroundwith(seekToBegin) _encoding = DetectEncoding(_stream);

			FileLength = _stream.Length;
            FileName = fileName;
            EndOffset = FileLength;
            OpenCommand = DelegateCommand(Open);
        }

        public FileLength : long { get; private set; }
        
        public FileName : string { get; private set; }

        [NotifyChangedOptions(Dependent = [StartText, Size])]
        public StartOffset : long { get; private set; }

        [NotifyChangedOptions(Dependent = [EndText, Size])]
        public EndOffset : long { get; private set; }

        public Size : long { get { EndOffset - StartOffset } }

        public OpenCommand : ICommand { get; private set; }

        public StartText : string 
        { 
            get 
            {
                ReadText(StartOffset);
			} 
        }
        
        public EndText : string 
        { 
            get
            {
               ReadText(EndOffset - _buffer.Length);
            }
        }
        
        private ReadText(startOffset : long) : string
        {
            def charLength = _encoding.GetByteCount("0");
            
            _ = _stream.Seek(startOffset - startOffset % charLength, SeekOrigin.Begin);
			def length = _stream.Read(_buffer, 0, _buffer.Length);
			_encoding.GetString(_buffer, 0, length);
        }

        private _stream : Stream;
        private _encoding : Encoding;
		private _buffer : array[byte] = array(400);
    }
}
