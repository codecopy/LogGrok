using Nemerle;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;
using Nemerle.WPF;

using System;
using System.Collections.Generic;
using System.Collections.Specialized;
using System.Collections.ObjectModel;
using System.ComponentModel;
using System.Linq;
using System.Text.RegularExpressions;
using System.Threading;
using System.Windows.Input;

using LogGrok.Core;
using LogGrok.FilterManager;
using LogGrok.LogView.DataVirtualization;
using LogGrok.LogView;
using LogGrok.Macros;

using Microsoft.Practices.Prism.Commands;
using Microsoft.Practices.Prism.PubSubEvents;

using Microsoft.Practices.Unity;
using System.Windows.Controls;

namespace LogGrok.Search
{
    [NotifyPropertyChanged]    
    public class SearchDocument : ILineViewModelProviderAcceptor
    {
        public this([NotNull] navigationService : ILogNavigation, 
                    [NotNull] filterManager : FilterManager, 
                    [NotNull] searchFilter : SearchFilter, 
                    [NotNull] highlighter : ILogHighlight,
                    [NotNull] scratchPad : IScratchPad,
                    [NotNull] gridViewFactory : LogGridViewFactory,
                    [NotNull] lineViewModelProviderFactory : IItemProvider[int] -> IItemProvider[LineViewModel],
                    [NotNull] syncContext : SynchronizationContext)
        {
            _navigationService = navigationService;
            
            filterManager.RegisterClient(this);
            _searchFilter = searchFilter;
            _highlighter = highlighter;
            _syncContext = syncContext;
            _gridViewFactory = gridViewFactory;
            _lineViewModelProviderFactory = lineViewModelProviderFactory;
            
            AddToScratchPad = DelegateCommand(o => (o : IEnumerable[object]).Cast.[LineViewModel]().Iter(x => scratchPad.AddText(x.RawLine)) );
        }
        
        public CustomView : ViewBase 
        {
            get
            {
                _gridViewFactory.CreateView(false)
            }
        }
      
        public SearchText : string
        {
            get { _searchText }; 
            set 
            { 
                unless (_searchText == value)
                {
                    _searchText = value; 
                          
                    StartSearch(); 
                }
            } 
        }

        public IsCaseSensitive : bool 
        { 
            get { _isCaseSensitive }; 
            set 
            { 
                unless (_isCaseSensitive == value)
                {
                    _isCaseSensitive = value;
                    StartSearch();
                }
                
            }; 
        }
        
        public UseRegex : bool 
        { 
            get { _useRegex }; 
            set 
            { 
                unless (_useRegex == value)
                {
                    _useRegex = value;
                    StartSearch();
                }
            }
        }
        
        [Throttle(TimeSpan.FromMilliseconds(500))]
        private StartSearch() : void
        {
            SearchProgress = 0;
            
            mutable options = SearchOptions.None;
            when (IsCaseSensitive)
                options |= SearchOptions.CaseSensitive;
            
            when (UseRegex) 
                options |= SearchOptions.UseRegex;
                
            _highlighter.SetTextForHighlight(SearchText, options);
            _searchFilter.Reset(SearchText, options);
        }
        
        public SearchResults : VirtualList[LineViewModel] { get; set; }

        [NotifyChangedOptions(Dependent = [IsIndeterninateProgress, IsSearching])]
        public SearchProgress : double { get; private set }
        
        public IsSearching : bool
        {
            get
            {
                SearchProgress < 100
            }
        }
        
        public IsIndeterninateProgress : bool
        {
            get
            {
                SearchProgress < 1
            }
        }
        
        public SelectedValue : LineViewModel 
        {
            get 
            { 
                _selectedValue; 
            }
            set 
            { 
                _selectedValue = value; 
                when (value != null)
                    _navigationService.NavigateTo(value.Index);
            }
        }

        ResetFilter() : void
            implements ILineViewModelProviderAcceptor.ResetFilter
        {
            
        }
        
        public SetItemProvider(itemProvider : DynamicFilteredLinesProvider) : void
        {
            _itemProvider = itemProvider;
            _itemProvider.ProgressChanged += _ => SearchProgress = _itemProvider.Progress;
            
            SearchResults  = VirtualList(_lineViewModelProviderFactory(itemProvider), _syncContext);
            SearchProgress = _itemProvider.Progress;
        }
        
        public AddToScratchPad : ICommand { get; private set; }
        
        private _navigationService : ILogNavigation;
        private mutable _selectedValue : LineViewModel;    
        private mutable _itemProvider : DynamicFilteredLinesProvider;
        private _lineViewModelProviderFactory : IItemProvider[int] -> IItemProvider[LineViewModel];
        private _searchFilter  : SearchFilter;
        private _syncContext   : SynchronizationContext;
        private _highlighter : ILogHighlight;
        private _gridViewFactory : LogGridViewFactory;
        
        private mutable _searchText    : string;
        private mutable _isCaseSensitive : bool;
        private mutable _useRegex : bool;
    }
}
